programa = {declaracao_funcao | tipo declaracao_var_com_atrib}. 

declaracao_funcao = tipo identificador "(" [declaracao_variavel {"," declaracao_variavel}] ")" {instrucao} "end".

declaracao_variavel = tipo identificador ";".

declaracao_var_com_atrib = identificador ["=" expressao] ";".

instrucao = tipo declaracao_var_com_atrib | "ret" retorno | "while" laco | "if" condicional | identificador "=" expressao ";" | identificador "(" [{expressao ","} expressao] ")" ";".

laco = expressao "do" {instrucao} "end".

condicional = expressao "then" {instrucao} ["else" {instrucao}] "end".

retorno = expressao ";".

tipo = "int" | "float" | "bool" | "char".

expressao = termo_e ["||" expressao].

termo_e = termo_igualdade ["&&" termo_e].

termo_igualdade = termo_relacional ["==" termo_igualdade] | termo_relacional ["!=" termo_igualdade].

termo_relacional = termo_aditivo [">" termo_relacional] | termo_aditivo ["<" termo_relacional] | termo_aditivo [">=" termo_relacional] | termo_aditivo ["<=" termo_relacional].

termo_aditivo = termo_multiplicativo ["+" termo_aditivo] | termo_multiplicativo ["-" termo_aditivo].

termo_multiplicativo = termo_primario ["*" termo_multiplicativo] | termo_primario ["/" termo_multiplicativo] | termo_primario ["mod" termo_multiplicativo].

termo_primario = identificador | constante_numero | constante_caractere | constante_booleano | identificador "(" [{expressao ","} expressao] ")" | "(" expressao ")".

imprimir = "print" expressao ";".
